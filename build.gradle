plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.12'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'ai.junbeom'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	// runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// 프론트엔드 태스크를 위한 작업 디렉토리 정의
def frontendDir = "$projectDir/frontend"

// 'npm install'을 실행하는 태스크
task installReact(type: Exec) {
    workingDir frontendDir
    inputs.files "$frontendDir/package.json", "$frontendDir/package-lock.json"
    outputs.dir "$frontendDir/node_modules"
    // 윈도우에서는 'npm.cmd' 사용
    commandLine 'npm', 'install'
}

// 'npm run build'를 실행하는 태스크
task buildReact(type: Exec, dependsOn: 'installReact') {
    workingDir frontendDir
    inputs.dir "$frontendDir/src"
    inputs.dir "$frontendDir/public"
    inputs.file "$frontendDir/vite.config.js"
    outputs.dir "$frontendDir/dist"
    // 윈도우에서는 'npm.cmd' 사용
    commandLine 'npm', 'run', 'build'
}

// 빌드된 React 앱을 Spring Boot의 'static/react' 폴더로 복사하는 태스크
task copyReactBuild(type: Copy, dependsOn: 'buildReact') {
    from "$frontendDir/dist"
    into "$projectDir/src/main/resources/static/react"
}

// copy 태스크를 빌드 프로세스에 연결
processResources.dependsOn(copyReactBuild)

tasks.named('bootRun') {
    dependsOn(copyReactBuild)
}

